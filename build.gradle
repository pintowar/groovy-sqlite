buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

version = '0.1.4'
group = 'com.github.groovy-sqlite'
sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile "org.codehaus.groovy:groovy:2.4.3"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
    testRuntime "com.h2database:h2:1.4.182"
}

task javadocJar( type: Jar, dependsOn: javadoc ) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar( type: Jar ) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    scm {
        url 'https://github.com/pintowar/groovy-sqlite'
        connection 'scm:https://github.com/pintowar/groovy-sqlite.git'
        developerConnection 'scm:git://github.com/pintowar/groovy-sqlite.git'
    }
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'pintowar'
            name 'Thiago Oliveira'
            email 'pintowar@gmail.com'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode( 'name', 'groovy-sqlite' )
                root.appendNode( 'description', 'Library for Groovy that mount queries with a simple configuration.' )
                root.appendNode( 'url', 'http://github.com/pintowar/groovy-sqlite/' )
                root.appendNode( 'inceptionYear', '2015' )
                root.children().last() + pomConfig
            }
        }
    }
}


bintray {
    user = !hasProperty('bintray_user') ? bintray_user : ''
    key = !hasProperty('bintray_api_key') ? bintray_api_key : ''
    publications = ['mavenJava'] // When uploading Maven-based publication files

    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'maven'
        name = project.name
        desc = 'Library for Groovy that mount queries with a simple configuration.'
        websiteUrl = 'https://github.com/pintowar/groovy-sqlite'
        issueTrackerUrl = 'https://github.com/pintowar/groovy-sqlite/issues'
        vcsUrl = 'https://github.com/pintowar/groovy-sqlite.git'
        licenses = ['Apache 2.0']
        labels = ['java', 'groovy', 'query']
        publicDownloadNumbers = true
    }
}

bintrayUpload.dependsOn(check)

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}
